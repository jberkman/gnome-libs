dnl
dnl Configure script for the Gnome library
dnl

AC_INIT(gnome.h)
AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE(gnome-libs, 0.13)

AM_MAINTAINER_MODE

dnl
dnl let applications configure for gnome
dnl
gnome_cv_use_gnome=yes

AC_ISC_POSIX
AC_PROG_CC
AC_HEADER_STDC
AC_ARG_PROGRAM
AM_PROG_LIBTOOL
GNOME_X_CHECKS

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

ALL_LINGUAS=""
AM_GNU_GETTEXT

AC_LINK_FILES($nls_cv_header_libgt, $nls_cv_header_intl)

AC_CHECK_LIB(m, main)

AC_CHECK_HEADERS(dlfcn.h dl.h)
AC_CHECK_LIB(dl, dlopen, DL_LIB="-ldl",[
AC_CHECK_LIB(dld, shl_load, DL_LIB="-ldld",[
AC_CHECK_FUNCS(dlopen, DL_LIB="",
AC_MSG_ERROR(Dynamic linking is not available on this platform.  Some apps,
like panel, will not run properly.))
])])
AC_SUBST(DL_LIB)

dnl
dnl The libgnome specific checks
dnl
AC_CHECK_FUNCS(strerror gethostbyname setfsgid)
NSL_LIBS=""
if test $ac_cv_func_gethostbyname = no; then
  AC_CHECK_LIB(nsl,gethostbyname,NSL_LIBS=-lnsl)
fi
AC_SUBST(NSL_LIBS)

dnl
dnl The libgnomeui specific checks
dnl

LIBGNOME_LIBS="$INTLLIBS $DL_LIB"
GNOME_LIBS="-lgnome $LIBGNOME_LIBS"

LIBGNOMEUI_LIBS="$GNOME_LIBS $GTK_LIBS $X_LIBS $x_libs"
GNOMEUI_LIBS="-lgnomeui $LIBGNOMEUI_LIBS"

LIBGTKXMHTML_LIBS="$GTK_LIBS $XPM_LIBS $JPEG_LIBS $PNG_LIBS $Z_LIBS $X_LIBS $x_libs"
GTKXMHTML_LIBS="-lgtkxmhtml $LIBGTKXMHTML_LIBS"

AC_SUBST(GNOME_LIBS)
AC_SUBST(GNOMEUI_LIBS)
AC_SUBST(GTKXMHTML_LIBS)

AC_SUBST(LIBGNOME_LIBS)
AC_SUBST(LIBGNOMEUI_LIBS)
AC_SUBST(LIBGTKXMHTML_LIBS)

dnl
dnl The Gtk/XmHTML code
dnl
Z_LIBS=""
JPEG_LIBS=""
PNG_LIBS=""
AC_CHECK_LIB(z, inflate, [Z_LIBS="-lz"])
AC_CHECK_LIB(jpeg, jpeg_abort, [JPEG_LIBS="-ljpeg"])
AC_CHECK_LIB(png, png_read_image, [PNG_LIBS="-lpng"])
xLIBS="$LIBS"
LIBS="$LIBS $GTK_LIBS $X_LIBS $x_libs"
AC_CHECK_FUNCS(gtk_signal_handler_pending)
LIBS="$xLIBS"
AC_SUBST(Z_LIBS)
AC_SUBST(JPEG_LIBS)
AC_SUBST(PNG_LIBS)

AC_CONFIG_SUBDIRS(libgtktty)

AC_OUTPUT([
gnomeConf.sh
Makefile
po/Makefile.in
intl/Makefile
gtk-xmhtml/Makefile
libgnome/Makefile
libgnomeui/Makefile
test-suite/Makefile
gnome-hello/Makefile
version.h
stamp.h],[sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])
