dnl
dnl Configure script for the Gnome library
dnl

AC_INIT(gnome.h)
AM_CONFIG_HEADER(config.h)

AC_DEFUN(AC_GNOME_X_CHECKS,
[
        AC_PATH_X
        AC_PATH_XTRA

        saved_cflags="$CFLAGS"
        saved_ldflags="$LDFLAGS"

        CFLAGS="$X_CFLAGS"
        LDFLAGS="$X_LDFLAGS $X_LIBS"

        dnl Checks for libraries.
        AC_CHECK_LIB(X11, XOpenDisplay,
                x_libs="$X_PRE_LIBS -lX11 $X_EXTRA_LIBS",
                [AC_MSG_ERROR(No X11 installed)],
                $X_EXTRA_LIBS)
	AC_SUBST(x_libs)

        LDFLAGS="$saved_ldflags $X_LDFLAGS $X_LIBS $x_libs"

        AC_CHECK_LIB(Xext, XShmAttach,
                x_libs="$x_libs -lXext", ,
                $x_libs)

	gnome_cv_passdown_x_libs="$x_libs"
	gnome_cv_passdown_X_LIBS="$X_LIBS"
	gnome_cv_passdown_X_CFLAGS="$X_CFLAGS"

        LDFLAGS="$saved_ldflags $X_LDFLAGS $X_LIBS"

	dnl Assume that if we have -lSM then we also have -lICE.
	AC_CHECK_LIB(SM, SmcSaveYourselfDone,
	        [AC_DEFINE(HAVE_LIBSM)
	        x_libs="$x_libs -lSM -lICE"], ,
		$x_libs -lICE)
		AM_CONDITIONAL(ENABLE_GSM,
			test "x$ac_cv_lib_SM_SmcSaveYourselfDone" = "xyes")

        AC_CHECK_LIB(gtk, gdk_pixmap_unref,
                GTK_LIBS="-lgtk -lgdk -lglib -lm",
                [AC_MSG_ERROR(Can not find a Gtk 0.99.1, probably you have an older version?)],
                -lgdk -lglib $x_libs -lm)
	AC_SUBST(GTK_LIBS)

	gnome_cv_passdown_GTK_LIBS="$GTK_LIBS"

	XPM_LIBS=""
	AC_CHECK_LIB(Xpm, XpmFreeXpmImage, [XPM_LIBS="-lXpm"], , $x_libs)
	AC_SUBST(XPM_LIBS)

	PTHREAD_LIB=""
	AC_CHECK_LIB(pthread, pthread_create, PTHREAD_LIB="-lpthread",
		[AC_CHECK_LIB(c_r, pthread_create, PTHREAD_LIB="-lc_r")])
	AC_SUBST(PTHREAD_LIB)

        CFLAGS="$saved_cflags $X_CFLAGS"
        LDFLAGS="$saved_ldflags"
])

AC_DEFUN(AC_CHECK_GNOME,
[
	AC_GNOME_X_CHECKS

	AC_CHECK_LIB(gnome, gnome_init, [GNOME_LIBS="-lgnome -lgnomeui"],[
		AC_MSG_ERROR(Gnome libraries not found)],[
		"$GTK_LIBS $x_libs"])	
])

AM_INIT_AUTOMAKE(gnome-libs, 0.12)

AM_MAINTAINER_MODE

AC_ISC_POSIX
AC_PROG_CC
AC_HEADER_STDC
AC_ARG_PROGRAM
AM_PROG_LIBTOOL
AC_GNOME_X_CHECKS

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

ALL_LINGUAS="cs it ko fi de fr es"
AM_GNU_GETTEXT

AC_LINK_FILES($nls_cv_header_libgt, $nls_cv_header_intl)

AC_CHECK_LIB(m, main)

AC_CHECK_HEADERS(dlfcn.h dl.h)
AC_CHECK_LIB(dl, dlopen, DL_LIB="-ldl",[
AC_CHECK_LIB(dld, shl_load, DL_LIB="-ldld",[
AC_CHECK_FUNCS(dlopen, DL_LIB="",
AC_MSG_ERROR(Dynamic linking is not available on this platform.  Some apps, like panel, will not run properly.))
])])
AC_SUBST(DL_LIB)

dnl
dnl The libgnome specific checks
dnl
AC_CHECK_FUNCS(strerror gethostbyname setfsgid)
NSL_LIBS=""
if test $ac_cv_func_gethostbyname = no; then
  AC_CHECK_LIB(nsl,gethostbyname,NSL_LIBS=-lnsl)
fi
AC_SUBST(NSL_LIBS)

dnl
dnl The libgnomeui specific checks
dnl

dnl
dnl The Gtk/XmHTML code
dnl
Z_LIBS=""
JPEG_LIBS=""
PNG_LIBS=""
AC_CHECK_LIB(z, inflate, [Z_LIBS="-lz"])
AC_CHECK_LIB(jpeg, jpeg_abort, [JPEG_LIBS="-ljpeg"])
AC_CHECK_LIB(png, png_read_image, [PNG_LIBS="-lpng"])
AC_SUBST(Z_LIBS)
AC_SUBST(JPEG_LIBS)
AC_SUBST(PNG_LIBS)

AC_CONFIG_SUBDIRS(libgtktty)

AC_OUTPUT([
Makefile
po/Makefile.in
intl/Makefile
gtk-xmhtml/Makefile
libgnome/Makefile
libgnomeui/Makefile
test-suite/Makefile
version.h
stamp.h],[sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])
